plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    ndkVersion "21.4.7075529"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                arguments '-DBUILD_SHARED_LIBS=OFF'
            }
        }
    }

    flavorDimensions "depth"
    productFlavors {
        full {
            dimension "depth"
            externalNativeBuild {
                cmake {
                    arguments +='-DFLAVOR_DEPTH=full'
                }
            }
        }
        only8bpc {
            dimension "depth"
            externalNativeBuild {
                cmake {
                    arguments +='-DFLAVOR_DEPTH=only8bpc'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.6.0"
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        doNotStrip '**.so'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation("com.github.bumptech.glide:glide:4.11.0") {
        exclude group: "com.android.support"
    }
    implementation "com.android.support:support-fragment"
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
}

def mavenVersion = '0.8.1'

afterEvaluate {
    publishing {
        publications {
            fullRelease(MavenPublication) {
                groupId = 'jp.co.link_u.library.glideavif'
                artifactId = 'glideavif'
                version = mavenVersion
                from components.fullRelease
            }
            only8bpcRelease(MavenPublication) {
                groupId = 'jp.co.link_u.library.glideavif'
                artifactId = 'glideavif-8bpc'
                version = mavenVersion
                from components.only8bpcRelease
            }
        }
        repositories {
            maven {
                url = uri("$rootDir/repository")
            }
        }
    }
}
